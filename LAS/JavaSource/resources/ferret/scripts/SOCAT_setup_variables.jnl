!  SOCAT_setup_variables.jnl
!  Special setup For SOCAT variables. Doesn't quite fit into an init script.

! Set up varibles for prop-prop plots


IF `($ferret_is_socat"0") EQ 0` THEN EXIT/SCRIPT 


DEFINE SYMBOL vtype = 0
LET gettype = ($ferret_plot_var_0)
DEFINE SYMBOL vtype = `gettype,RETURN=dtype`
DEFINE SYMBOL is_string = ($vtype"0|CHAR>1|*>0")

IF ($is_string"0|0|*>1") THEN 
   IF `STRINDEX("($ferret_plot_var_0)", "WOCE") GT 0` THEN 
      LET plot_var_0 = STRFLOAT(($ferret_plot_var_0) )
      IF ($its_listing"0|*>1") THEN LET plot_var_0 = ($ferret_plot_var_0)
      DEFINE SYMBOL ferret_plot_var_0 = plot_var_0
      LET/TITLE="($ferret_title_0)" xvar = plot_var_0
   ENDIF
   IF `STRINDEX("($ferret_plot_var_0)", "tmonth") GT 0` THEN 
      LET month_labels = {"Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec"}
      LET plot_var_0 =  element_index_str_n( ($ferret_plot_var_0), month_labels) 
      DEFINE SYMBOL ferret_plot_var_0 = plot_var_0
      LET/TITLE="($ferret_title_0)" xvar = plot_var_0
   ENDIF
ENDIF

IF ($ferret_plot_var_1"0|*>1") THEN
   DEFINE SYMBOL vtype = 0
   LET gettype = ($ferret_plot_var_1)
   DEFINE SYMBOL vtype = `gettype,RETURN=dtype`
   DEFINE SYMBOL is_string = ($vtype"0|CHAR>1|*>0")
   IF ($is_string"0|0|*>1") THEN 
      IF `STRINDEX("($ferret_plot_var_1)", "WOCE") GT 0` THEN 
         LET plot_var_1 = STRFLOAT(($ferret_plot_var_1) )
         IF ($its_listing"0|*>1") THEN LET plot_var_1 = ($ferret_plot_var_1)
         DEFINE SYMBOL ferret_plot_var_1 = plot_var_1
         LET/TITLE="($ferret_title_1)" yvar = plot_var_1
      ENDIF
      IF `STRINDEX("($ferret_plot_var_1)", "tmonth") GT 0` THEN 
         LET month_labels = {"Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec"}
         LET plot_var_1 =  element_index_str_n( ($ferret_plot_var_1), month_labels) 
         DEFINE SYMBOL ferret_plot_var_1 = plot_var_1
         LET/TITLE="($ferret_title_1)" yvar = plot_var_1
      ENDIF
   ENDIF
ENDIF

IF ($data_0_var_in"0|region_ID>1|qc_flag>1|*>0") THEN 
   SET VAR/NAME=xvar_0 xvar
   GO SOCAT_custom_vars ($data_0_var_in) xvar_0
   DEFINE SYMBOL ferret_plot_var_0 = color_var
   LET/TITLE="($ferret_title_0)" xvar = color_var
ENDIF

IF ($data_1_var_in"0|region_ID>1|qc_flag>1|*>0") THEN 
   SET VAR/NAME=yvar_0 yvar
   GO SOCAT_custom_vars ($data_1_var_in) yvar_0
   DEFINE SYMBOL ferret_plot_var_1 = color_var
   LET/TITLE="($ferret_title_1)" yvar = color_var
ENDIF


IF `($data_count"0") GE 3` THEN ! color by, or 3rd variable

   IF `($data_2_var_in"0|*>1") EQ 0` THEN DEFINE SYMBOL data_2_var_in = ($data_2_var)

   DEFINE SYMBOL vtype = 0
   LET gettype = ($ferret_plot_var"($data_2_var)")
   DEFINE SYMBOL vtype = `gettype,RETURN=dtype`
   DEFINE SYMBOL is_string = ($vtype"0|CHAR>1|*>0")

! Woce flags come in a string, with un-set ones a blank. Count this as "bad".
   IF ($is_string"0|0|*>1") THEN 
      IF `STRINDEX("($ferret_plot_var)", "WOCE") GT 0` THEN 
         SET VAR/NAME = zvar_0 zvar
         LET plot_var_2 = STRFLOAT(zvar_0 )
         LET plot_var   = STRFLOAT(zvar_0 )
         IF ($its_listing"0|*>1") THEN LET plot_var_2 = ($ferret_plot_var_2)
         IF ($its_listing"0|*>1") THEN LET plot_var = ($ferret_plot_var_2)
         DEFINE SYMBOL ferret_plot_var_2 = plot_var_2
         DEFINE SYMBOL ferret_plot_var   = plot_var
         LET zvar = plot_var_2
         SET ATT/LIKE=zvar_0 zvar

         LET getgood = STRFLOAT (($data_var))
         DEFINE SYMBOL ngood_data = `getgood[i=@ngd]`
         DEFINE SYMBOL nbad_data = `getgood[i=@nbd]`

      ENDIF

      IF `STRINDEX("($ferret_plot_var)", "tmonth") GT 0` THEN 
         LET month_labels = {"Jan", "Feb", "Mar", "Apr", "May", "Jun", "Jul", "Aug", "Sep", "Oct", "Nov", "Dec"}
         SET VAR/NAME = zvar_0 zvar
         LET plot_var_2 =  element_index_str_n(zvar_0, month_labels) 
         LET plot_var   =  element_index_str_n(zvar_0, month_labels) 
         DEFINE SYMBOL ferret_plot_var_2 = plot_var_2
         DEFINE SYMBOL ferret_plot_var   = plot_var
         LET zvar = plot_var_2
         SET ATT/LIKE=zvar_0 zvar
         DEFINE SYMBOL ferret_fill_levels = (0.5,12.5,1)
         KEYMARK 0
      ENDIF

      IF ($data_2_var_in"0|region_ID>1|qc_flag>1|*>0") THEN 
         SET VAR/NAME = zvar_1 zvar
         GO SOCAT_custom_vars ($data_2_var_in) zvar_1
         LET zvar = color_var
         DEFINE SYMBOL ferret_plot_var_2  = color_var
         DEFINE SYMBOL ferret_plot_var = color_var
         SET ATT/LIKE=zvar_1 zvar
      ENDIF
      
   ENDIF
ENDIF

! For SOCAT server labels, make time symbols w/o the trailing 00:00:00

IF ($region_0_t_lo"0|*>1") THEN
   DEFINE SYMBOL t_lab_lo = ($region_0_t_lo)
   LET iz = STRINDEX("($region_0_t_lo)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_lo)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_lo = `SUBSTRING("($region_0_t_lo)", 1, iz-1)`
   ENDIF
ENDIF 

IF `STRCMP("($region_0_t_lo)", "($region_0_t_hi)") EQ 0` THEN  EXIT/SCRIPT

IF ($region_0_t_hi"0|*>1") THEN
   DEFINE SYMBOL t_lab_hi = ($region_0_t_hi)
   LET iz = STRINDEX("($region_0_t_hi)", " 00:00:00")
   IF `iz GT 0` THEN 
      DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ELSE
      LET iz = STRINDEX("($region_0_t_hi)", ":00:00")
      IF `iz GT 0` THEN DEFINE SYMBOL t_lab_hi = `SUBSTRING("($region_0_t_hi)", 1, iz-1)`
   ENDIF
ENDIF

!-----------------End of SOCAT_setup_variables.jnl ---------------------------------
