! SOCAT_custom_levs_keys.jnl
! Special setup for colorbars and levels for some variables:
! QC_FLAG (needs to be expanded)
! region_id
! SOCAT version
! Apply auto levels when no decimation, pre-determined levels when decimated.
!  5/2019 get the socat version from attributes and use it to set the levels for socat_version plots
!  5/29/2019 Set the color palettes in the dataset xml, which will supersede any settings made here.


DEFINE SYMBOL var_name = $1

! Did they send in request to use standard pre-defined levels?

LET use_std_levels = 1

IF `($ferret_trajectory_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_trajectory_palette)


LET lon_range = ($region_x_range)
LET lat_range = ($region_y_range)
LET fland_area = `ABS(lon_range * lat_range) / (360 * 180)`

IF `($piecewise_decimation"0|*>1") EQ 0 AND fland_area LT 0.1` THEN LET use_std_levels = 0
IF `($numobs"0|bad>0|*") GT 100000` THEN  LET use_std_levels = 1
IF ($ferret_fill_levels"0|*>1") THEN LET use_std_levels = 0

LET levels_string = UPCASE("($ferret_fill_levels%0%)")

LET have_standard_levels = {"fCO2_recommended","fCO2_water_equi_uatm","pCO2_water_equi_temp","xCO2_water_equi_temp_dry_ppm"}
IF `IS_ELEMENT_OF_STR_N("($var_name)", have_standard_levels) EQ 0` THEN
   IF `STRINDEX(levels_string, "STAN") GT 0` THEN 
      CANCEL SYMBOL ferret_fill_levels
      LET levels_string = 0
   ENDIF
ENDIF

! Time palette. Settings for the levels and key are done elsewhere
IF ($var_name"0|time>1|*>0") THEN
   IF `($ferret_palette"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_palette = inverse_medium_rainbow
ENDIF

! Note etopo is positive numbers indicating depth.
IF ($var_name"0|etopo2>1|*>0") THEN
   IF `($ferret_palette"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_palette = pale_green_blue
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = (0,7000,200)(inf)
   IF `use_std_levels EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = v,0min
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
ENDIF

IF ($var_name"0|latitude>1|*>0") THEN
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN 
      DEFINE SYMBOL ferret_fill_levels = 30
      LET lrange = MINMAX(latitude)
      IF `lrange[i=2] - lrange[i=1] LT 30` THEN \
       DEFINE SYMBOL ferret_fill_levels = `INT(lrange[i=2] - lrange[i=1] +1)`
      IF `lrange[i=2] - lrange[i=1] LT 10` THEN DEFINE SYMBOL ferret_fill_levels = 20
   ENDIF
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
   IF `lrange,RETURN=isready` THEN CANCEL VARIABLE lrange

   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0
ENDIF

IF ($var_name"0|longitude>1|*>0") THEN
   IF `(($ferret_fill_levels"0|*>1") EQ 0)` THEN
      IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = 30
      LET lrange = MINMAX(longitude)
      IF `lrange[i=2] - lrange[i=1] LT 30` THEN \
      DEFINE SYMBOL ferret_fill_levels = `INT(lrange[i=2] - lrange[i=1] +1)`
      IF `lrange[i=2] - lrange[i=1] LT 10` THEN DEFINE SYMBOL ferret_fill_levels = 20
      IF `lrange,RETURN=isready` THEN CANCEL VARIABLE lrange
   ENDIF
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0
ENDIF

IF ($var_name"0|gvCO2>1|*>0") THEN
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = 30
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
ENDIF

IF ($var_name"0|fCO2_source>1|*>0") THEN
   IF `($ferret_palette"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_palette = fifteen_by_levels

   IF `($ferret_version) GE 6.92` THEN 
      IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = (1,15,1)
      DEFINE SYMBOL ferret_key_centerlab = 1
   ELSE
      IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = (0.5,14.5,1)
   ENDIF
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0
ENDIF

! Redefine socat_version as a float. Set its levels.

IF ($var_name"0|socat_version>1|*>0") THEN
   DEFINE SYMBOL dtype = `socat_version,RETURN=dtype`
   IF ($dtype"|CHAR>1|*>0") THEN
      SET VAR/NAME=socat_version_in socat_version
      LET socat_version = STRFLOAT(socat_version_in)
      LET DATA_VAR_EXPAND = EXPND_BY_LEN(socat_version, ($rowvar),`($rowvar)[m=@sum]`)
      DEFINE SYMBOL data_var = data_var_expand
      LET var_with_gaps = SEPARATE(DATA_VAR_EXPAND[i=1:`npts`], ($rowvar), 0)  
      DEFINE SYMBOL data_var = var_with_gaps
      CANCEL SYMBOL is_string
   ENDIF

   IF `($ferret_palette"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_palette = ten_bright_levels  
   DEFINE SYMBOL ferret_fill_levels = (1.3)(1.4)(2,6,1)(2019)

! Get this SOCAT version from the dataset title.  If we're beyond 2019, define levels 
! with this year as the highest. Starting in 2022, will need a palette with more than 
! 10 levels, say fifteen_by_levels, 

   LET vpos = STRINDEX("($dataset_title_att)", "SOCAT v")
   DEFINE SYMBOL dset_version = `SUBSTRING ("($dataset_title_att)", vpos+7, 4)`
   IF `STRFLOAT("($dset_version)") GT 2019` THEN DEFINE SYMBOL ferret_fill_levels = (1.3)(1.4)(2,6,1)(2019,($dset_version),1)

   DEFINE SYMBOL ferret_key_centerlab = 1
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0
ENDIF


! Note sample_number is positive numbers indicating count on trajectory.
IF ($var_name"0|sample_number>1|*>0") THEN
   IF `use_std_levels EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = 30
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
ENDIF

! This based on the full-range socat2 thru 2011

IF ($var_name"0|fCO2_recommended>1|*>0") THEN
   IF `STRINDEX(levels_string, "STAN") GT 0` THEN 
      LET use_std_levels = 1
      CANCEL SYMBOL ferret_fill_levels
   ENDIF
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN \
     DEFINE SYMBOL ferret_fill_levels = (-inf)(160,260,20)(260,450,10)(440,560,20)(inf)
ENDIF

! Set the rest of the fCO2, pCO2, and xCO2 the same 
IF ($var_name"0|fCO2_water_equi_uatm>1|fCO2_water_sst_100humidity_uatm>1|*>0") THEN
   IF `STRINDEX(levels_string, "STAN") GT 0` THEN 
      LET use_std_levels = 1
      CANCEL SYMBOL ferret_fill_levels
   ENDIF
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN \
      DEFINE SYMBOL ferret_fill_levels = (-inf)(160,260,20)(260,450,10)(440,560,20)(inf)
ENDIF

IF ($var_name"0|pCO2_water_equi_temp>1|pCO2_water_sst_100humidity_uatm>1|*>0") THEN
   IF `STRINDEX(levels_string, "STAN") GT 0` THEN 
      LET use_std_levels = 1
      CANCEL SYMBOL ferret_fill_levels
   ENDIF
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN \
      DEFINE SYMBOL ferret_fill_levels = (-inf)(160,260,20)(260,450,10)(440,560,20)(inf)
ENDIF

IF ($var_name"0|xCO2_water_equi_temp_dry_ppm>1|xCO2_water_sst_dry_ppm>1|*>0") THEN
   IF `STRINDEX(levels_string, "STAN") GT 0` THEN 
      LET use_std_levels = 1
      CANCEL SYMBOL ferret_fill_levels
   ENDIF
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN \
      DEFINE SYMBOL ferret_fill_levels = (-inf)(160,260,20)(260,450,10)(440,560,20)(inf)
ENDIF

! Atmospheric carbon levels
IF ($var_name"0|fCO2_atm>1|pCO2_atm>1|*>0") THEN
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN \
      DEFINE SYMBOL ferret_fill_levels = (-inf)(355,390,2)(inf)
ENDIF
IF ($var_name"0|xCO2_atm>1|*>0") THEN
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN \
      DEFINE SYMBOL ferret_fill_levels = (-inf)(355,410,2)(inf)
ENDIF

! delta 

IF ($var_name"0|delta_fCO2>1|delta_pCO2>1|delta_temp>1|delta_xCO2>1|*>0") THEN
   IF `($ferret_palette"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_palette = centered_diff
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = vc
ENDIF

! Direction variables all in degrees

IF ($var_name"0|ship_dir>1|wind_dir_rel>1|wind_dir_true>1|*>0") THEN
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN 
      DEFINE SYMBOL ferret_fill_levels = (0,360,10)
   ELSE
      DEFINE SYMBOL ferret_fill_levels = 30,0min,360max
   ENDIF
ENDIF

! Speed variables positive

IF ($var_name"0|calc_speed>1|ship_speed>1|wind_speed_rel>1|wind_speed_true>1|*>0") THEN
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels =v,0min
ENDIF

! day_of_year

IF ($var_name"0|day_of_year>1|*>0") THEN
   IF `use_std_levels AND (($ferret_fill_levels"0|*>1") EQ 0)` THEN 
      DEFINE SYMBOL ferret_fill_levels = (0,366,6)
   ELSE
      DEFINE SYMBOL ferret_fill_levels = (0,366,6)
      LET lrange = MINMAX(day_of_year)
      IF `lrange[i=2] - lrange[i=1] LT 30` THEN \
       DEFINE SYMBOL ferret_fill_levels = `INT(lrange[i=2] - lrange[i=1] +1)`
      IF `lrange[i=2] - lrange[i=1] LT 10` THEN DEFINE SYMBOL ferret_fill_levels = 20
   ENDIF
ENDIF

! dist_to_land

IF ($var_name"0|dist_to_land>1|*>0") THEN
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN \
      DEFINE SYMBOL ferret_fill_levels = (0,1000,25)(inf)
ENDIF


! tmonth
IF ($var_name"0|tmonth>1|*>0") THEN
   IF `($ferret_version) GE 6.92` THEN 
      IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = (1,12,1)
      DEFINE SYMBOL ferret_key_centerlab = 1
   ELSE
      IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = ferret_fill_levels = (0.5,12.5,1)
   ENDIF
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0
ENDIF

IF `STRINDEX("($var_name)", "WOCE") GT 0` THEN
   DEFINE SYMBOL dtype = `($var_name),RETURN=dtype`
   IF ($dtype"|CHAR>1|*>0") THEN
      SET VAR/NAME=($var_name)_in ($var_name)
      LET ($var_name) = STRFLOAT(($var_name)_in)
      LET var_with_gaps = SEPARATE(($var_name), ($rowvar), 0)
      DEFINE SYMBOL data_var = var_with_gaps
      CANCEL SYMBOL is_string
   ENDIF

   IF `($ferret_trajectory_palette"0|*>1")` THEN DEFINE SYMBOL ferret_palette =  ($ferret_trajectory_palette)
   IF `($ferret_palette"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_palette =  woce2
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = (2,4,1)
   IF `($ferret_plot_key"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = 1
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate 0
   IF `($ferret_version) GE 6.92` THEN DEFINE SYMBOL ferret_key_centerlab = 1
ENDIF

IF ($var_name"0|qc_flag>1|*>0") THEN
   DEFINE SYMBOL do_qc_key = 1
   IF `($ferret_plot_key"0|0|1>0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = qc_flag
   DEFINE SYMBOL ferret_palette = QC_V3_by_level
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0
   IF `($ferret_version) GE 6.92` THEN 
      DEFINE SYMBOL ferret_fill_levels = (0,10,1)
      DEFINE SYMBOL ferret_key_centerlab = 1
   ELSE
      IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = (0.5,10.5,1)
   ENDIF
ENDIF

IF ($var_name"0|region_ID>1|*>0") THEN
   DEFINE SYMBOL ferret_palette = QC_regions
   IF `($ferret_fill_levels"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_fill_levels = (1,8,1)
   DEFINE SYMBOL ferret_key_centerlab = 1
   IF `($ferret_plot_key"0|0|1>0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_plot_key = region_id
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0

   DEFINE SYMBOL  drawthick = 2  ! work around a Ferret bug with thick=3 and by-levels palette. Top color showed as black
ENDIF

! Large number of color levels, but not larger than # of features.

IF ($var_name"|($feature_variable)>1|*>0") THEN
   IF ($ferret_fill_levels"1|*>0") THEN 
      DEFINE SYMBOL ferret_fill_levels = 55
      IF `nfeatures LT 55` THEN DEFINE SYMBOL ferret_fill_levels = `nfeatures`
      IF `nfeatures LT 30` THEN DEFINE SYMBOL ferret_fill_levels = (1,`nfeatures`,1)
   ENDIF
   IF `($ferret_key_annotate"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_key_annotate = 0
   IF `($ferret_plot_key"0|*>1") NE 1` THEN DEFINE SYMBOL ferret_plot_key = 0
   IF `($ferret_palette"0|*>1") EQ 0` THEN DEFINE SYMBOL ferret_palette = ten_bright_levels
ENDIF


! End of file ------------ SOCAT_custom_levs_keys.jnl--------------------------
