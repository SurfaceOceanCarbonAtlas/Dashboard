! see
! http://www.esrl.noaa.gov/gmd/ccgg/globalview/co2/co2_version.html  says,
!
!    The Reference Marine Boundary Layer matrix, which was provided in previous 
!    releases, is no longer available. Instead, NOAA has made a more comprehensive 
!    MBL reference product available here.  

! The link references this page :
! http://www.esrl.noaa.gov/gmd/ccgg/mbl/
!
! and from there, http://www.esrl.noaa.gov/gmd/ccgg/mbl/data.php, downloading 
!the "surface" reference type gives us sine(latitude) vs time of the Reference 
! Marine Boundary Layer. 
!
! This file is in this format: 
! # Format = (F12.6,82(1X,F12.4))
! TIME  DATA  UNCERTAINTY  DATA  UNCERTAINTY, ...
! e.g.
!  1979.000000     334.6322       0.0912     334.6365       0.0975
! The data set lies in the YT plane.
! Y is on equal intervals of SIN(latitude) = -1:1:0.05
! The date field is in year fractions -- an ugly unit for Ferret, since years are not all of equal length. 

let nskip = 78
define symbol infile = co2_GHGreference.1065549360_surface.txt
define symbol outfile = gvco2_2017.nc

file/col=1/format="(1x,F12.6)"/var=date/skip=`nskip` ($infile)
let ndate = `date[i=@ngd]`
list/prec=9/i=1:5 date
list/prec=9/i=`ndate-5`:`ndate` date
pause

LET tdate = TSEQUENCE(date)
LET tcyears = INT(tdate)
LET days_in_year = IF ( (ABS(MOD(tcyears,4)) GT 1.0E-7) OR \
                        ((ABS(MOD(tcyears,100)) LT 1.0E-7) AND (ABS(MOD(tcyears,400)) GT 1.0E-7)) ) \
                   THEN 365.0 ELSE 366.0
LET tcdays1970 = DAYS1900(tcyears,1,1) + (tdate - tcyears) * days_in_year - DAYS1900(1970,1,1)
DEFINE AXIS /T0=01-JAN-1970 /UNIT=days tcarbon = tcdays1970
show axis tcarbon
list/prec=9 tdate[gt=tcarbon@ASN],_T[gt=tcarbon]
pause

! define the latitude axis as 41 equal intervals of SIN(Y)
let sinlat = y[y=-1:1:.05]
define axis/y/units=degrees lats = asin(sinlat) * 180 / 3.14159

! define the grid of the dataset and set it up
define grid/y=lats/t=tcarbon gcarbon

! 13x skips the time coordinate. 14x skips the uncertainty values

file/skip=`nskip`/format="(13x, 41(F12.4,14x))"/col=41/var=co2_in/grid=gcarbon ($infile)

! Units and title.
let/title="NOAA greenhouse gas MBL surface reference 2017"/units="umol/mol" co2 = co2_in
shade/lev=(325,412,.5) co2
pause

save/clobber/file="($outfile)" co2

! The co2 values are followed by their uncertainty
cancel data/all

file/skip=`nskip`/format="(13x, 41(14x,F12.4))"/col=41/var=unc_in/grid=gcarbon ($infile)

let/title="Uncertainty, NOAA greenhouse gas MBL surface reference 2017"/units="umol/mol" uncertain = unc_in
shade uncertain
pause

save/append/file="($outfile)" uncertain

can dat/all
can var/all

! Look at dataset differences
use "($outfile)"
show grid co2
use "/home/data/socat/gvco2_2016.nc"
show grid co2
use "/home/data/socat/gvco2_2015.nc"
show grid co2
use "/home/data/socat/gvco2_2014.nc"
show grid co2
use "/home/data/socat/gvco2_2013.nc"
show grid co2
use "/home/data/socat/gvco2_2012.nc"
show grid co2
use "/home/data/socat/gvco2_2008.nc"
show grid co2
set data 1

let/title="gvc02 2017" co2_17 = co2[d=1,gt=co2[d=1]]
let/title="gvc02 2016" co2_16 = co2[d=2,gt=co2[d=1]]
let/title="gvc02 2015" co2_15 = co2[d=3,gt=co2[d=1]]
let/title="gvc02 2014" co2_14 = co2[d=4,gt=co2[d=1]]
let/title="gvc02 2013" co2_13 = co2[d=5,gt=co2[d=1]]
let/title="gvc02 2012" co2_12 = co2[d=6,gt=co2[d=1]]
let/title="gvc02 2008" co2_08 = co2[d=7,gt=co2[d=1]]

! Difference between what we had as gvco2 2017 and 2016
set win 1
shade /pal=red_blue_centered /lev=20cv co2_17 - co2_16
stats co2_17 - co2_16

! Difference between what we had as gvco2 2016 and 2015
set win 2
shade /pal=red_blue_centered /lev=20cv co2_16 - co2_15
stats co2_16 - co2_15

! Difference between what we had as gvco2 2015 and 2014
set win 3
shade /pal=red_blue_centered /lev=20cv co2_15 - co2_14
stats co2_15 - co2_14

! Difference between what we had as gvco2 2014 and 2013
set win 4
shade /pal=red_blue_centered /lev=20cv co2_14 - co2_13
stats co2_14 - co2_13

! Difference between what we had as gvco2 2013 and 2012
set win 5
shade /pal=red_blue_centered /lev=20cv co2_13 - co2_12
stats co2_13 - co2_12

! Difference between what we had as gvco2 2012 and 2008
set win 6
shade /pal=red_blue_centered /lev=20cv co2_12 - co2_08
stats co2_12 - co2_08

pause

