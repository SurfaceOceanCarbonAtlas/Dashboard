! preview_map.jnl
!

CANCEL MODE nodata_lab
DEFINE VIEW/AXES/X=0.08:0.99/y=0.06:0.92 full_view
DEFINE VIEW/AXES/X=0.08:0.99/y=0.06:0.92 full_view2

LET/UNITS="`longitude,RETURN=units`"/TITLE="`longitude,RETURN=title`" lon = longitude
LET/UNITS="`latitude,RETURN=units`"/TITLE="`latitude,RETURN=title`" lat = latitude

LET lonmin = `min(lon[i=@MIN]-5, 180)`
LET lonmax = `max(lon[i=@MAX]+5, -180)`
GO preview_xy_range lonmin lonmax 100

LET xax_lo = ($ppl$range_low)
LET xax_hi = ($ppl$range_high)
LET del_xax = ($ppl$range_inc)

LET varnames = ..varnames
IF `IS_ELEMENT_OF_STR(varnames, "lon360") GT 0` THEN
   
   LET lonmin = `min(lon360[i=@MIN]-5, 360)`
   LET lonmax = `max(lon360[i=@MAX]+5, 0)`
   
   GO preview_xy_range lonmin lonmax 

   LET ax_lo360 = ($ppl$range_low)
   LET ax_hi360 = ($ppl$range_high)
   LET xax360_del = ($ppl$range_inc)

   IF `(ax_hi360 - ax_lo360) LT (xax_hi-xax_lo)` THEN
      LET/UNITS="`longitude,RETURN=units`"/TITLE="`longitude,RETURN=title`" lon = lon360
      LET xax_lo = ax_lo360
      LET xax_hi = ax_hi360
      LET del_xax = xax360_del
      DEFINE SYMBOL use_360 = 1
   ENDIF

ENDIF

LET latmin = `max(lat[i=@MIN]-5, -90)`
LET latmax = `min(lat[i=@MAX]+5,  90)`

GO preview_xy_range latmin latmax

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)
LET del_yax = ($ppl$range_inc)

! IF there's almost all land (mooring in Siberia?) or almost no land (South Pacific?)
! then expand the map boundaries.  Do with coarse etopo20, avoid details like lakes.

use etopo20
LET all_grid = 1+0*rose
LET land_flag = if rose GT 0 then 1  else 0
LET land_pct = land_flag[x=`xax_lo`:`xax_hi`@sum,y=`yax_lo`:`yax_hi`@sum] 

LET count = 0
REPEAT/RANGE=1:5 (\
LET land_pct = land_flag[x=`xax_lo`:`xax_hi`@sum,y=`yax_lo`:`yax_hi`@sum]/all_grid[x=`xax_lo`:`xax_hi`@sum,y=`yax_lo`:`yax_hi`@sum];\
SAY `land_pct` in-range `land_pct GT 0.01 AND land_pct LT 0.99`; \
IF `land_pct GT 0.01 AND land_pct LT 0.95` THEN EXIT/LOOP;\
LET lonmin = `lonmin-15`;\
LET lonmax = `lonmax+15`;\
GO preview_xy_range lonmin lonmax 100;\
LET xax_lo = ($ppl$range_low);\
LET xax_hi = ($ppl$range_high);\
LET del_xax = ($ppl$range_inc);\
LET latmin = `MAX(-90,latmin-15)`;\
LET latmax = `MIN(90,latmax+15)`;\
GO preview_xy_range latmin latmax;\
LET yax_lo = ($ppl$range_low);\
LET yax_hi = ($ppl$range_high);\
LET del_yax = ($ppl$range_inc);\
LET count = `count+1`;\
) 

! Shrink any huge lon range to 360
IF `xax_hi - xax_lo GE 360` THEN
   IF ($use_360"0|*>1") THEN
      LET xax_hi = 360
      LET xax_lo = 0
   ELSE
      LET xax_hi = 180
      LET xax_lo = -180
   ENDIF
ENDIF

LET map_land_pct = `land_pct`
CANCEL DATA etopo20
SET DATA 1

DEFINE SYMBOL connect_dots = 1

IF `ABS(xax_hi - xax_lo) LT 1` THEN DEFINE SYMBOL dm_labels = dm
IF `ABS(xax_hi - xax_lo) LT 0.06` THEN DEFINE SYMBOL dm_labels = dms
DEFINE AXIS/x=`xax_lo`:`xax_hi`:`del_xax`/units="($lon_units%degrees_east%)" haxis

DEFINE AXIS/y=`yax_lo`:`yax_hi`:`del_yax`/units=deg vaxis

LET dep_var = x[gx=haxis]
LET indep_var = y[gy=vaxis]
LET bvar = dep_var + indep_var
LET allmiss = IF bvar THEN (`bvar,RETURN=bad`)

SHADE/NOKEY/PAL=white/title=" "/SET allmiss
   ppl xlab
   ppl ylab
PPL SHADE

DEFINE SYMBOL region_x_lo = `xax_lo`
DEFINE SYMBOL region_x_hi = `xax_hi`
DEFINE SYMBOL region_x_range = `xax_hi - xax_lo`
DEFINE SYMBOL region_y_lo = `yax_lo`
DEFINE SYMBOL region_y_hi = `yax_hi`
DEFINE SYMBOL region_y_range = `yax_hi - yax_lo`

GO preview_fland ($land_palette)


PLOT/over/vs/LINE/DASH=(0.1,0.04,0.1,0.04)/line=6/NOLAB lon, lat


! color the path with woce flag
DEFINE SYMBOL symquals = ($ppstyle)
STAT lon
LET lonconst = `($stat_std) LT 0.1`
STAT lat
LET latconst = `($stat_std) LT 0.1`
IF `lonconst + latconst EQ 2` THEN DEFINE SYMBOL symquals = /SYM=17/SIZE=0.15/THICK=2

RIBBON/over/vs/missing=blank/NOLAB($symquals)/key/PALETTE=($woce_palette)/levels=(2,4,1)/KEY=CENT lon, lat, WOCE_CO2_water
