! Difference between source and recalculated fCO_2
! 9/2017. Check for no-valid fco2_recommended
!         Previously checked fco2_source. It is set to -1 for no valid data

GO preview_setup.jnl ($infile_dsg) 13

! We compute the fco2_source in calcSOCAT3.jnl
!  (Note that the fCO2_insitu_from_xCO2_water_equi_temp_dry_ppm may not exist, even
!  if the values of xCO2_water_equi_temp_dry_ppm are valid, because the computed
!  fCO2_insitu variable also depends on temperature and temperature_equi.)
!  Some of these are duplicates - e.g. fCO2_from_pCO2_water_water_equi_temp and
!  fCO2_from_pCO2_water_water_equi_temp_ncep both come from pCO2_water_water_equi_temp

LET vsrc1 = IF fco2_source EQ 1 THEN xCO2_water_equi_temp_dry_ppm ELSE 0
LET vsrc2 = IF fco2_source EQ 2 THEN xCO2_water_sst_dry_ppm ELSE 0
LET vsrc3 = IF fco2_source EQ 3 THEN pCO2_water_equi_temp ELSE 0
LET vsrc4 = IF fco2_source EQ 4 THEN pCO2_water_sst_100humidity_uatm ELSE 0
LET vsrc5 = IF fco2_source EQ 5 THEN fCO2_water_equi_uatm ELSE 0
LET vsrc6 = IF fco2_source EQ 6 THEN fCO2_water_sst_100humidity_uatm ELSE 0
LET vsrc7 = IF fco2_source EQ 7 THEN pCO2_water_equi_temp ELSE 0
LET vsrc8 = IF fco2_source EQ 8 THEN pCO2_water_sst_100humidity_uatm ELSE 0
LET vsrc9 = IF fco2_source EQ 9 THEN xCO2_water_equi_temp_dry_ppm ELSE 0
LET vsrc10 = IF fco2_source EQ 10 THEN xCO2_water_sst_dry_ppm ELSE 0
LET vsrc11 = IF fco2_source EQ 11 THEN xCO2_water_equi_temp_dry_ppm ELSE 0
LET vsrc12 = IF fco2_source EQ 12 THEN xCO2_water_sst_dry_ppm ELSE 0
LET vsrc13 = IF fco2_source EQ 13 THEN xCO2_water_equi_temp_dry_ppm ELSE 0
LET vsrc14 = IF fco2_source EQ 14 THEN xCO2_water_sst_dry_ppm ELSE 0


LET sources = vsrc1 + vsrc2 + vsrc3 + vsrc4 + vsrc5 + vsrc6 + vsrc7 + vsrc8 + vsrc9 + vsrc10 + vsrc11 + vsrc12 + vsrc13 + vsrc14

LET mask = IF fco2_source THEN 1

LET masksource = sources* mask

LET diff = fco2_recommended - masksource
LET diff_t = reshape( diff, tt)
LET fco2_t = reshape(fco2_recommended, tt)
LET source_t = reshape (masksource, tt)
LET alg_id_t = reshape (fco2_source, tt)

SET VIEW FULL

LET nsrc = `fco2_recommended[i=@ngd]`
IF `nsrc EQ 0` THEN
  plot/nolab/vs/hlim=0:1/vlim=0:1/set {100,200},{100,200}
  ppl axlabp,0,0
  ppl plot
  LABEL/nouser `($ppl$xlen)/2`,`($ppl$ylen)/2`, 0, 0, 0.15, (NO VALID DATA)
ELSE

LET use_color = `fco2_source[i=@min] NE fco2_source[i=@max]`

! Compute the range, draw the plot centered on zero.
let drange = ABS(minmax(diff_t))
let amin = `-1.1*drange[i=@max]`
let amax = ` 1.1*drange[i=@max]`

GO preview_xy_range amin amax 1

LET yax_lo = ($ppl$range_low)
LET yax_hi = ($ppl$range_high)

 ! Not interested in tiny differences
IF `drange[i=@max] - drange[i=@min] LT 0.5` THEN   
   LET yax_lo = -1
   LET yax_hi = 1
ENDIF

DEFINE SYMBOL vlimits = /vlim=`yax_lo`:`yax_hi`


IF `use_color` THEN
   ribbon($linestyle)($vlimits)/PAL=thirty_by_levels/LEV=(1,15,1)/KEY=cent/\
   TITLE="@SRrecommended fCO_2 minus source CO_2<NL>Colored by source id "/SET diff_t, alg_id_t
     ppl ylab "Difference (`fco2_recommended,return=units`)"
     ppl xlab " "
     ($taxis_setting)
     IF ($labnum_year"0|*>1") THEN GO unlabel ($labnum_year)
     IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
     IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
   PPL RIBBON
ELSE 
   PLOT($linestyle)($vlimits)/COLOR=blue/SET diff_t
     ppl ylab "Difference (`fco2_recommended,return=units`)"
     ppl xlab " "
     DEFINE SYMBOL src = `fco2_source[i=@min]`
     PPL title "recommended fCO_2 minus source CO_2<NL>Single source is  `source_labels[i=($src)]`"
     ($taxis_setting)
     IF ($labnum_year"0|*>1") THEN GO unlabel ($labnum_year)
     IF ($labnum_dset"0|*>1") THEN go unlabel ($labnum_dset)
     IF ($labnum_datitl"0|*>1") THEN go unlabel ($labnum_datitl)
   PPL PLOT
ENDIF

plot/over/color=6/dash=(0.15,0.05,0.15,0.05)/nolab 0*tt

ENDIF  ! fco2_source all missing

! Label at the top with the Expocode and count
DEFINE SYMBOL lab_top = Expocode `EXPOCODE`
LABEL/NOUSER  `($ppl$xlen)/2`,`($ppl$ylen)+0.6*($ppl$yorg)`, 0,0,0.13,  ($lab_top)
label/nouser  `($ppl$xlen)/2`,`($ppl$ylen)+0.3*($ppl$yorg)`, 0,0,0.10,  `nfnoc` Valid fCO_2 recommended

FRAME($trans)/FORMAT=gif/FILE="($result_plot_image_filename)"
