! dump_gridded_monthly.jnl
!
! Creates a spreadsheet-like dump of the contents of SOCAT_tracks_gridded_monthly.nc as SOCAT_tracks_gridded_monthly.txt
! Uses ncdump to dump the metadata preamble - goes to stdout
! Then lists the axes - goes to stdout
! Then lists the first point to get the header - goes to stdout
! Then lists the actual data - goes to SOCAT_tracks_gridded_monthly.txt
!
! Edit the information printed to stdout and then append the actual data printout
!

CANCEL MODE VERIFY
SET MEMORY/SIZE=8G
CANCEL MODE UPCASE
SET MODE DATE_FORMAT ISO

SPAWN ncdump -h SOCAT_tracks_gridded_monthly.nc

SAY

USE SOCAT_tracks_gridded_monthly.nc
SET GRID fco2_ave_weighted
SHOW GRID

LET /TITLE="T-Axis Date" tax_date = TAX_DATESTRING(_T[g=fco2_ave_weighted], fco2_ave_weighted, "day")
LOAD /PERM tax_date, count_ncruise

SAY
SAY ******************************************************************************
SAY **** NOTE: rows containing no data (cruise count is zero) are not printed ****
SAY ******************************************************************************
SAY

LIST /QUIET /PREC=0 /WIDTH=5000 /HEAD /NOROWLAB /I=1 /J=1 /L=1 \
    tax_date, _Y, _X, count_ncruise, \
    fco2_count_nobs, fco2_ave_weighted, fco2_ave_unwtd, fco2_min_unwtd, fco2_max_unwtd, \
    sst_count_nobs, sst_ave_weighted, sst_ave_unwtd, sst_min_unwtd, sst_max_unwtd, \
    salinity_count_nobs, salinity_ave_weighted, salinity_ave_unwtd, salinity_min_unwtd, salinity_max_unwtd

SAY
SAY Missing values are represented as "...."
SAY

SPAWN rm -f SOCAT_tracks_gridded_monthly.txt
SET LIST /FILE=SOCAT_tracks_gridded_monthly.txt
REPEAT /L=1:`count_ncruise,RETURN=LEND` ( \
    IF `count_ncruise[X=@MAX,Y=@MAX]` THEN \
        SAY L = `_L` ;\
        REPEAT /J=1:`count_ncruise,RETURN=JEND` ( \
            IF `count_ncruise[X=@MAX]` THEN \
                SAY J = `_J` ;\
                LOAD fco2_count_nobs, fco2_ave_weighted, fco2_ave_unwtd, fco2_min_unwtd, fco2_max_unwtd, \
                     sst_count_nobs, sst_ave_weighted, sst_ave_unwtd, sst_min_unwtd, sst_max_unwtd, \
                     salinity_count_nobs, salinity_ave_weighted, salinity_ave_unwtd, salinity_min_unwtd, salinity_max_unwtd ;\
                REPEAT /I=1:`count_ncruise,RETURN=IEND` ( \
                    IF `count_ncruise` THEN  \
                        LIST /QUIET /FILE /APPEND /PREC=6 /WIDTH=5000 /NOHEAD /NOROWLAB \
                            tax_date, _Y, _X, count_ncruise, \
                            fco2_count_nobs, fco2_ave_weighted, fco2_ave_unwtd, fco2_min_unwtd, fco2_max_unwtd, \
                            sst_count_nobs, sst_ave_weighted, sst_ave_unwtd, sst_min_unwtd, sst_max_unwtd, \
                            salinity_count_nobs, salinity_ave_weighted, salinity_ave_unwtd, salinity_min_unwtd, salinity_max_unwtd \
                ) \
        ) \
)

