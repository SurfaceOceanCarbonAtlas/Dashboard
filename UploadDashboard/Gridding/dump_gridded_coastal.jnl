! dump_gridded_coastal.jnl
!
! Creates a spreadsheet-like dump of the contents of SOCAT_qrtrdeg_gridded_coast_monthly.nc as SOCAT_qrtrdeg_gridded_coast_monthly.txt
! Uses ncdump to dump the metadata preamble - goes to stdout
! Then lists the axes - goes to stdout
! Then lists the first point to get the header - goes to stdout
! Then lists the actual data - goes to SOCAT_qrtrdeg_gridded_coast_monthly.txt
!
! Edit the information printed to stdout and then append the actual data printout
!

CANCEL MODE VERIFY
SET MEMORY/SIZE=12G
CANCEL MODE UPCASE
SET MODE DATE_FORMAT ISO

SPAWN ncdump -h SOCAT_qrtrdeg_gridded_coast_monthly.nc

SAY

USE SOCAT_qrtrdeg_gridded_coast_monthly.nc
SET GRID coast_fco2_ave_weighted
SHOW GRID

LET /TITLE="T-Axis Date" tax_date = TAX_DATESTRING(_T[g=coast_fco2_ave_weighted], coast_fco2_ave_weighted, "day")
LOAD /PERM tax_date, coast_count_ncruise

SAY
SAY ******************************************************************************
SAY **** NOTE: rows containing no data (cruise count is zero) are not printed ****
SAY ******************************************************************************
SAY

LIST /QUIET /PREC=0 /WIDTH=5000 /HEAD /NOROWLAB /I=1 /J=1 /L=1 \
    tax_date, _Y, _X, coast_count_ncruise, \
    coast_fco2_count_nobs, coast_fco2_ave_unwtd, coast_fco2_ave_weighted, \
    coast_fco2_max_unwtd, coast_fco2_min_unwtd, coast_fco2_std_unwtd, coast_fco2_std_weighted, \
    coast_sst_count_nobs, coast_sst_ave_unwtd, coast_sst_ave_weighted, \
    coast_sst_max_unwtd, coast_sst_min_unwtd, coast_sst_std_unwtd, coast_sst_std_weighted, \
    coast_salinity_count_nobs, coast_salinity_ave_unwtd, coast_salinity_ave_weighted, \
    coast_salinity_max_unwtd, coast_salinity_min_unwtd, coast_salinity_std_unwtd, coast_salinity_std_weighted, \
    coast_lat_offset_unwtd, coast_lon_offset_unwtd \

SAY
SAY Missing values are represented as "...."
SAY

SPAWN rm -f SOCAT_qrtrdeg_gridded_coast_monthly.txt
SET LIST /FILE=SOCAT_qrtrdeg_gridded_coast_monthly.txt
REPEAT /L=1:`coast_count_ncruise,RETURN=LEND` ( \
    IF `coast_count_ncruise[X=@MAX,Y=@MAX]` THEN \
        SAY L = `_L` ;\
        REPEAT /J=1:`coast_count_ncruise,RETURN=JEND` ( \
            IF `coast_count_ncruise[X=@MAX]` THEN \
                SAY J = `_J` ;\
                LOAD coast_fco2_count_nobs, coast_fco2_ave_unwtd, coast_fco2_ave_weighted, \
                     coast_fco2_max_unwtd, coast_fco2_min_unwtd, coast_fco2_std_unwtd, coast_fco2_std_weighted, \
                     coast_sst_count_nobs, coast_sst_ave_unwtd, coast_sst_ave_weighted, \
                     coast_sst_max_unwtd, coast_sst_min_unwtd, coast_sst_std_unwtd, coast_sst_std_weighted, \
                     coast_salinity_count_nobs, coast_salinity_ave_unwtd, coast_salinity_ave_weighted, \
                     coast_salinity_max_unwtd, coast_salinity_min_unwtd, coast_salinity_std_unwtd, coast_salinity_std_weighted, \
                     coast_lat_offset_unwtd, coast_lon_offset_unwtd ;\
                REPEAT /I=1:`coast_count_ncruise,RETURN=IEND` ( \
                    IF `coast_count_ncruise` THEN \
                        LIST /QUIET /FILE /APPEND /PREC=6 /WIDTH=5000 /NOHEAD /NOROWLAB \
                            tax_date, _Y, _X, coast_count_ncruise, \
                            coast_fco2_count_nobs, coast_fco2_ave_unwtd, coast_fco2_ave_weighted, \
                            coast_fco2_max_unwtd, coast_fco2_min_unwtd, coast_fco2_std_unwtd, coast_fco2_std_weighted, \
                            coast_sst_count_nobs, coast_sst_ave_unwtd, coast_sst_ave_weighted, \
                            coast_sst_max_unwtd, coast_sst_min_unwtd, coast_sst_std_unwtd, coast_sst_std_weighted, \
                            coast_salinity_count_nobs, coast_salinity_ave_unwtd, coast_salinity_ave_weighted, \
                            coast_salinity_max_unwtd, coast_salinity_min_unwtd, coast_salinity_std_unwtd, coast_salinity_std_weighted, \
                            coast_lat_offset_unwtd, coast_lon_offset_unwtd \
                ) \
        ) \
)

