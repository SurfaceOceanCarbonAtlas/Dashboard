!
! Add global title attribute
can mode upcase
IF ($file_qual"0|CLOBBER>1|*>0") THEN GO add_global_attribute ($gridded_outfile)
DEFINE SYMBOL file_qual = APPEND


DEFINE AXIS/T0="01-JAN-1970"/EDGES/UNITS=days tmnth = DAYS1900(year,month,1) - offset1970
show axis tmnth 

! Count of cruises with some data in each XYT cell
! Send a scalar as argument 1 to just count the cruises
LET var = 1
LET/UNITS="count"/TITLE="Number of cruises"/BAD=-9999 \
  count_ncruise = tracks2grid_mask_ave_xyt(var,LON,LAT,DATE,CRUISE_NO,X[GX=xlon],Y[GY=ylat],T[GT=tmnth],maskvar)

SAVE/($file_qual)/FILE="($gridded_outfile)"/OUTTYPE=INT count_ncruise

! Now count all of observations in each XYT cell, mean weighted by cruise,
! unweighted mean, min, max, variance, unweighted std for fco2, sst, salinity.

! Define these symbols for the script stats_var to use.
! var		the variable to use.
! titlename	text to use in the title, e.g. fCO2
! vname		text to use in creating variable names, e.g. lowercase fco2

CANCEL SYMBOL quarter

DEFINE SYMBOL var = fco2_rec
DEFINE SYMBOL titlename = fCO2
DEFINE SYMBOL vname = fco2
GO stats_var

DEFINE SYMBOL var = sst
DEFINE SYMBOL titlename = SST
DEFINE SYMBOL vname = sst
GO stats_var

DEFINE SYMBOL var = salinity
DEFINE SYMBOL titlename = Salinity
DEFINE SYMBOL vname = salinity
GO stats_var

! Mean delta-latitude of all observations in each XYT cell
LET nearest_lat = IF lat GE 0 THEN (INT(lat) + 0.5) ELSE (INT(lat) - 0.5)
LET y_from_lat = (lat - nearest_lat)
LET/UNITS="Deg N"/TITLE="Latitude average offset from cell center" \
  lat_offset_unwtd = SCAT2GRID_BIN_XYT(LON,LAT,DATE,y_from_lat,X[GX=xlon],Y[GY=ylat],T[GT=tmnth])

SAVE/APPEND/OUTTYPE=float/FILE="($gridded_outfile)" lat_offset_unwtd

! Mean delta-longitude of all observations in each XYT cell
LET nearest_lon = IF lon GE 0 THEN (INT(lon) + 0.5) ELSE (INT(lon) - 0.5)
LET x_from_lon = (lon - nearest_lon) 
LET/UNITS="Deg E"/TITLE="Longitude average offset from cell center" \
  lon_offset_unwtd = SCAT2GRID_BIN_XYT(LON,LAT,DATE,x_from_lon,X[GX=xlon],Y[GY=ylat],T[GT=tmnth])

SAVE/APPEND/OUTTYPE=float/FILE="($gridded_outfile)" lon_offset_unwtd
sh axis tmnth

