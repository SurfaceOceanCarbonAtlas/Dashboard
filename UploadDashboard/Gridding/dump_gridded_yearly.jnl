! dump_gridded_yearly.jnl
!
! Creates a spreadsheet-like dump of the contents of SOCAT_tracks_gridded_yearly.nc as SOCAT_tracks_gridded_yearly.txt
! Uses ncdump to dump the metadata preamble - goes to stdout
! Then lists the axes - goes to stdout
! Then lists the first point to get the header - goes to stdout
! Then lists the actual data - goes to SOCAT_tracks_gridded_yearly.txt
!
! Edit the information printed to stdout.
! Remove the '----' lines from SOCAT_tracks_gridded_yearly.txt and add spaces after the commas using 
!    grep -v -- ---- SOCAT_tracks_gridded_yearly.txt | sed -e 's/,/, /g' > (outputfilename)
! Concatenate the two edited files to create the CSV file.
! Edit the information printed to stdout.
!

CANCEL MODE VERIFY
SET MEMORY/SIZE=4G
CANCEL MODE UPCASE
SET MODE DATE_FORMAT ISO

SPAWN ncdump -h SOCAT_tracks_gridded_yearly.nc

SAY

USE SOCAT_tracks_gridded_yearly.nc
SET GRID count_ncruise_year
SHOW GRID

SAY
SAY ******************************************************************************
SAY **** NOTE: rows containing no data (cruise count is zero) are not printed ****
SAY ******************************************************************************
SAY
SAY Column  1: DATE is ISO Date representing the year midpoint (axis TYEAR)
SAY Column  2: LAT is Latitude (axis YLAT)
SAY Column  3: LON is Longitude (axis XLON)
SAY

LIST /QUIET /PREC=0 /WIDTH=5000 /HEAD /NOROWLAB /I=1 /J=1 /L=1 \
    _T, _Y, _X, count_ncruise_year, \
    fco2_count_nobs_year, fco2_ave_weighted_year, fco2_ave_unwtd_year, fco2_min_unwtd_year, fco2_max_unwtd_year, \
    sst_count_nobs_year, sst_ave_weighted_year, sst_ave_unwtd_year, sst_min_unwtd_year, sst_max_unwtd_year, \
    salinity_count_nobs_year, salinity_ave_weighted_year, salinity_ave_unwtd_year, salinity_min_unwtd_year, salinity_max_unwtd_year

SAY
SAY Missing values are represented as -1.E+34
SAY

SET MODE VERIFY

LET linear_ncruise = XSEQUENCE(count_ncruise_year)
LET mask = IF linear_ncruise THEN 1
LOAD mask
DEFINE SYMBOL num_good = `mask[X=@NGD]`

LET my_time_year = _T + 0.0 * _X + 0.0 * _Y
SAVE /FILE=my_time_year.nc /CLOBBER my_time_year

LET /TITLE="Time" /UNIT="days since 1900-01-01" valid_time_year = COMPRESSI_BY(XSEQUENCE(my_time_year), mask)
SAVE /I=1:($num_good) /FILE=valid_time_year.nc /CLOBBER valid_time_year
CANCEL VAR valid_time_year, my_time_year

LET my_lat_year = _Y + 0.0 * _X + 0.0 * _T
LET /TITLE="Latitude" /UNIT="degrees_north" valid_lat_year = COMPRESSI_BY(XSEQUENCE(my_lat_year), mask)
SAVE /I=1:($num_good) /FILE=valid_lat_year.nc /CLOBBER valid_lat_year
CANCEL VAR valid_lat_year, my_lat_year

LET my_lon_year = _X + 0.0 * _Y + 0.0 * _T
LET /TITLE="Longitude" /UNIT="degrees_east" valid_lon_year = COMPRESSI_BY(XSEQUENCE(my_lon_year), mask)
SAVE /I=1:($num_good) /FILE=valid_lon_year.nc /CLOBBER valid_lon_year
CANCEL VAR valid_lon_year, my_lon_year

LET vararray = { \
    "count_ncruise_year", \
    "fco2_count_nobs_year", \
    "fco2_ave_weighted_year", \
    "fco2_ave_unwtd_year", \
    "fco2_min_unwtd_year", \
    "fco2_max_unwtd_year", \
    "sst_count_nobs_year", \
    "sst_ave_weighted_year", \
    "sst_ave_unwtd_year", \
    "sst_min_unwtd_year", \
    "sst_max_unwtd_year", \
    "salinity_count_nobs_year", \
    "salinity_ave_weighted_year", \
    "salinity_ave_unwtd_year", \
    "salinity_min_unwtd_year", \
    "salinity_max_unwtd_year" \
}

REPEAT /RANGE=1:16 /NAME=idx (\
   DEFINE SYMBOL varidx = `idx` ;\
   DEFINE SYMBOL varname = `vararray[I=($varidx)]` ;\
   LET /TITLE="`($varname),RETURN=TITLE`" /UNIT="`($varname),RETURN=UNIT`" valid_($varname) = COMPRESSI_BY(XSEQUENCE(($varname)), mask) ;\
   SAVE /I=1:($num_good) /FILE=valid_($varname).nc /CLOBBER valid_($varname) ;\
   CANCEL VAR valid_($varname) ;\
   CANCEL SYMBOL varname ;\
   CANCEL SYMBOL varidx \
)

SET GRID abstract
CANCEL DATA /ALL
CANCEL SYMBOL /ALL
CANCEL VAR /ALL
CANCEL MEMORY /ALL

USE my_time_year.nc

LET dataset_array = { \
    "valid_time_year.nc", \
    "valid_lat_year.nc",  \
    "valid_lon_year.nc", \
    "valid_count_ncruise_year.nc", \
    "valid_fco2_count_nobs_year.nc", \
    "valid_fco2_ave_weighted_year.nc", \
    "valid_fco2_ave_unwtd_year.nc", \
    "valid_fco2_min_unwtd_year.nc", \
    "valid_fco2_max_unwtd_year.nc", \
    "valid_sst_count_nobs_year.nc", \
    "valid_sst_ave_weighted_year.nc", \
    "valid_sst_ave_unwtd_year.nc", \
    "valid_sst_min_unwtd_year.nc", \
    "valid_sst_max_unwtd_year.nc", \
    "valid_salinity_count_nobs_year.nc", \
    "valid_salinity_ave_weighted_year.nc", \
    "valid_salinity_ave_unwtd_year.nc", \
    "valid_salinity_min_unwtd_year.nc", \
    "valid_salinity_max_unwtd_year.nc" \
}

DEFINE DATA /AGGREGATE /U valid_data = dataset_array

LET valid_tax_date = TAX_DATESTRING(valid_time_year, my_time_year[d=1], "day")

LIST /ORDER=TYX /QUIET /FILE=SOCAT_tracks_gridded_yearly.txt /CLOBBER /FORMAT=comma /PREC=6 /WIDTH=5000 /NOHEAD /NOROWLAB \
    valid_tax_date, valid_lat_year, valid_lon_year, valid_count_ncruise_year, \
    valid_fco2_count_nobs_year, valid_fco2_ave_weighted_year, valid_fco2_ave_unwtd_year, valid_fco2_min_unwtd_year, valid_fco2_max_unwtd_year, \
    valid_sst_count_nobs_year, valid_sst_ave_weighted_year, valid_sst_ave_unwtd_year, valid_sst_min_unwtd_year, valid_sst_max_unwtd_year, \
    valid_salinity_count_nobs_year, valid_salinity_ave_weighted_year, valid_salinity_ave_unwtd_year, valid_salinity_min_unwtd_year, valid_salinity_max_unwtd_year

