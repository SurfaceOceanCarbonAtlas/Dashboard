! dump_gridded_yearly.jnl
!
! Creates a spreadsheet-like dump of the contents of SOCAT_tracks_gridded_yearly.nc as SOCAT_tracks_gridded_yearly.txt
! Uses ncdump to dump the metadata preamble - goes to stdout
! Then lists the axes - goes to stdout
! Then lists the first point to get the header - goes to stdout
! Then lists the actual data - goes to SOCAT_tracks_gridded_yearly.txt
!
! Edit the information printed to stdout and then append the actual data printout
!

CANCEL MODE VERIFY
SET MEMORY/SIZE=6G
CANCEL MODE UPCASE
SET MODE DATE_FORMAT ISO

SPAWN ncdump -h SOCAT_tracks_gridded_yearly.nc

SAY

USE SOCAT_tracks_gridded_yearly.nc
SET GRID fco2_ave_weighted_year
SHOW GRID

LET /TITLE="T-Axis Date" tax_date = TAX_DATESTRING(_T[g=fco2_ave_weighted_year], fco2_ave_weighted_year, "day")
LOAD /PERM tax_date, count_ncruise_year

SAY
SAY ******************************************************************************
SAY **** NOTE: rows containing no data (cruise count is zero) are not printed ****
SAY ******************************************************************************
SAY

LIST /QUIET /PREC=0 /WIDTH=5000 /HEAD /NOROWLAB /I=1 /J=1 /L=1 \
    tax_date, _Y, _X, count_ncruise_year, \
    fco2_count_nobs_year, fco2_ave_weighted_year, fco2_ave_unwtd_year, fco2_min_unwtd_year, fco2_max_unwtd_year, \
    sst_count_nobs_year, sst_ave_weighted_year, sst_ave_unwtd_year, sst_min_unwtd_year, sst_max_unwtd_year, \
    salinity_count_nobs_year, salinity_ave_weighted_year, salinity_ave_unwtd_year, salinity_min_unwtd_year, salinity_max_unwtd_year

SAY
SAY Missing values are represented as "...."
SAY

SPAWN rm -f SOCAT_tracks_gridded_yearly.txt
SET LIST /FILE=SOCAT_tracks_gridded_yearly.txt
REPEAT /L=1:`count_ncruise_year,RETURN=LEND` ( \
    IF `count_ncruise_year[X=@MAX,Y=@MAX]` THEN \
        SAY L = `_L` ;\
        REPEAT /J=1:`count_ncruise_year,RETURN=JEND` ( \
            IF `count_ncruise_year[X=@MAX]` THEN \
                SAY J = `_J` ;\
                LOAD fco2_count_nobs_year, fco2_ave_weighted_year, fco2_ave_unwtd_year, fco2_min_unwtd_year, fco2_max_unwtd_year, \
                     sst_count_nobs_year, sst_ave_weighted_year, sst_ave_unwtd_year, sst_min_unwtd_year, sst_max_unwtd_year, \
                     salinity_count_nobs_year, salinity_ave_weighted_year, salinity_ave_unwtd_year, salinity_min_unwtd_year, salinity_max_unwtd_year ;\
                REPEAT /I=1:`count_ncruise_year,RETURN=IEND` ( \
                    IF `count_ncruise_year` THEN \
                        LIST /QUIET /FILE /APPEND /PREC=6 /WIDTH=5000 /NOHEAD /NOROWLAB \
                            tax_date, _Y, _X, count_ncruise_year, \
                            fco2_count_nobs_year, fco2_ave_weighted_year, fco2_ave_unwtd_year, fco2_min_unwtd_year, fco2_max_unwtd_year, \
                            sst_count_nobs_year, sst_ave_weighted_year, sst_ave_unwtd_year, sst_min_unwtd_year, sst_max_unwtd_year, \
                            salinity_count_nobs_year, salinity_ave_weighted_year, salinity_ave_unwtd_year, salinity_min_unwtd_year, salinity_max_unwtd_year \
                ) \
        ) \
)

